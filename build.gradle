repositories.jcenter();

apply plugin: 'sonar-runner';

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    //buildDir = "$rootProject.projectDir/build"
}

def getCheckedOutCommitHash() {
    // https://gist.github.com/JonasGroeger/7620911
    def gitFolder = '.git'
    def shortHashSz = 7 //12

    def head = file("$gitFolder/HEAD").text.split(':')

    if (head.length == 1) // Detached head, hash is in HEAD file
        head[0].trim().take(shortHashSz)
    else { // length == 2, 'ref: refs/heads/...'
        def refHead = file("$gitFolder/${head[1].trim()}")
        refHead.text.trim().take shortHashSz
    }
}

version = getCheckedOutCommitHash()





// ----- SonarQube configuration ----- //

if (!project.hasProperty('sonarHost'))
    ext.sonarHost = 'localhost'

if (!project.hasProperty('sonarPortHttp'))
    ext.sonarPortHttp = '9000'

if (!project.hasProperty('sonarPortJdbc'))
    ext.sonarPortJdbc = '9092'

sonarRunner {
    sonarProperties {
        // Put server path in ~/.gradle/gradle.properties or -PsonarHost=[...]
        property 'sonar.host.url', "http://$sonarHost:$sonarPortHttp"
        property "sonar.jdbc.url", "jdbc:h2:tcp://$sonarHost:$sonarPortJdbc/sonar"
        if (project.hasProperty('sonarUser') && project.hasProperty('sonarPass')) {
            property "sonar.jdbc.username", sonarUser
            property "sonar.jdbc.password", sonarPass
        }
    }
}

project(':common') {
    apply plugin: 'jacoco'
    sonarRunner {
        sonarProperties {
            property 'sonar.junit.reportsPath', "$buildDir/test-results"
            property 'sonar.java.coveragePlugin', 'jacoco'
            property 'sonar.jacoco.reportPath', "$buildDir/jacoco/test.exec"
        }
    }
    tasks.getByPath(':sonarRunner').dependsOn ':common:jacocoTestReport'
}


project(':android-app') {
    sonarRunner {
        sonarProperties {
            // For directory tangle index, binaries should be analyzed too,
            // but we have dex... - http://stackoverflow.com/a/20348653

            property 'sonar.sources', android.sourceSets.main.java.srcDirs
            //property 'sonar.tests', android.sourceSets.test.java.srcDirs

            // This is not my code
            property "sonar.exclusions", "**/com/google/**"
        }
    }
}
