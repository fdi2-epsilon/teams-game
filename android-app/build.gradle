project.description = 'Android application'

buildscript {
    repositories.jcenter()
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

apply plugin: 'com.android.application'

repositories.jcenter()

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId 'eu.unipv.epsilon.enigma'
        minSdkVersion 19
        targetSdkVersion 22

        versionCode 1
        versionName '1.0'
    }

    buildTypes {
        debug { debuggable true }
        release { minifyEnabled false }
    }
}

dependencies {
    compile project(':common')
    // compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:22.1.0'
    compile 'com.android.support:cardview-v7:22.1.0'
    compile 'org.slf4j:slf4j-android:1.7.12'
}

// Show deprecation message details
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

// Evaluation of this project depends on each levels subproject,
// so we can access its tasks.
project(':levels').subprojects.each { evaluationDependsOn it.path }

// Collect files to send to device
task collectLevels(group: 'install', type: Copy) {
    def levelProjects = project(':levels').subprojects

    dependsOn levelProjects.collect { it.tasks.pack }
    from levelProjects.collect { it.tasks.pack.archivePath }
    into "$buildDir/adb-push/collections"
}

task uploadLevels(group: 'install', type: Exec, dependsOn: collectLevels) {
    def appId = android.defaultConfig.applicationId
    commandLine 'adb', 'push', '-p', "$buildDir/adb-push", "/data/data/$appId/files"
}
